buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

if(!invtweaks_version) {
    invtweaks_version = "UNKNOWN"
}

ext.build_number = System.getenv("BUILD_NUMBER")

if(build_number)  {
    version = invtweaks_version + "-" + build_number
} else {
    version = invtweaks_version
}
group = "inventorytweaks" // No stupid URL conventions
archivesBaseName = "InventoryTweaks"

ext.mcversion = "1.7.2"
minecraft {
    version = "1.7.2-10.12.1.1060"
    at 'src/main/resources/invtweaks_at.cfg'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

compileJava {
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'invtweaks.forge.asm.FMLPlugin', 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task apiJar(type: Jar) {
    dependsOn processResources

    from sourceSets.main.allJava
    include 'invtweaks/api/**'

    appendix = 'api'
}

task deobfJar(type: Jar) {
    dependsOn classes

    from sourceSets.main.output

    appendix = 'deobf'
}

artifacts {
    archives apiJar
    archives deobfJar
}

processResources
{
    inputs.property "version", version
    inputs.property "mcversion", mcversion

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.png'
        exclude '**/*_at.cfg'

        expand version: version, mcversion: mcversion
    }
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.png'
        include '**/*_at.cfg'
    }
}
