buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
    }
}

apply plugin: 'forge'

if(!invtweaks_version) {
    invtweaks_version = "UNKNOWN"
}

ext.build_number = System.getenv("BUILD_NUMBER")

if(build_number)  {
    version = invtweaks_version + "-" + build_number
} else {
    version = invtweaks_version
}
group = "inventorytweaks" // No stupid URL conventions
archivesBaseName = "InventoryTweaks"

minecraft {
    version = "1.7.2-10.12.0.996"
    at 'src/minecraft/invtweaks_at.cfg'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

sourceSets {
    main {
        java {
            srcDir 'src/minecraft'
        }
        resources {
            srcDir 'src/minecraft'
            exclude '**/*.java'
        }
    }
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'invtweaks.forge.asm.FMLPlugin', 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task apiJar(type: Jar) {
    dependsOn processResources

    from sourceSets.main.allJava
    include 'invtweaks/api/**'

    appendix = 'api'
}

task deobfJar(type: Jar) {
    dependsOn classes

    from sourceSets.main.output

    appendix = 'deobf'
}

artifacts {
    archives apiJar
    archives deobfJar
}

processResources
{
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.png'

//        expand ''
    }
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.png'
    }
}
