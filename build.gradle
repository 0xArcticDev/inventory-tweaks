buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'idea'
apply plugin: 'forge'
apply plugin: 'curseforge'

if(!invtweaks_version) {
    invtweaks_version = "UNKNOWN"
}

def getGitCommitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch(ignored) {
        return null;
    }
}

def getGitDirtyIndex = { ->
    try {
        return exec {
            commandLine 'git', 'diff-index', '--quiet', '--cached', 'HEAD'
            ignoreExitValue = true
        }.exitValue == 1
    } catch(ignored) {
        return false;
    }
}

def getGitDirtyFiles = { ->
    try {
        return exec {
            commandLine 'git', 'diff-files', '--quiet'
            ignoreExitValue = true
        }.exitValue == 1
    } catch(ignored) {
        return false;
    }
}

def getGitChangelog = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        def gitHash = System.getenv("GIT_COMMIT")
        def gitPrevHash = System.getenv("GIT_PREVIOUS_COMMIT")
        if(gitHash && gitPrevHash) {
            exec {
                commandLine 'git', 'log', '--pretty=tformat:%s - %aN', '' + gitPrevHash + '...' + gitHash
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } else {
            return "";
        }
    } catch(ignored) {
        return "";
    }
}

ext.git_version = getGitCommitHash()
ext.git_dirty = getGitDirtyIndex() || getGitDirtyFiles()

ext.build_number = System.getenv("BUILD_NUMBER")

if(build_number) {
    version = invtweaks_version + "-" + build_number
} else {
    version = invtweaks_version
}

if(git_version) {
    ext.internal_version = version + "-" + git_version
    if(git_dirty) {
        ext.internal_version += "-dirty"
    }
} else {
    ext.internal_version = version
}

group = "inventorytweaks" // No stupid URL conventions
archivesBaseName = "InventoryTweaks"

ext.mcversion = "1.8"
minecraft {
    version = "1.8-11.14.0.1296"
    mappings = 'snapshot_20150201'
    at 'src/main/resources/invtweaks_at.cfg'

    replace "@VERSION@", project.internal_version
    replaceIn "InvTweaksConst.java"
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

compileJava {
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'invtweaks.forge.asm.FMLPlugin', 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task apiJar(type: Jar) {
    dependsOn processResources

    from sourceSets.main.allJava
    include 'invtweaks/api/**'

    appendix = 'api'
}

task deobfJar(type: Jar) {
    dependsOn classes

    from sourceSets.main.output

    manifest {
        attributes 'FMLCorePlugin': 'invtweaks.forge.asm.FMLPlugin', 'FMLCorePluginContainsFMLMod': 'true'
    }

    appendix = 'deobf'
}

artifacts {
    archives apiJar
    archives deobfJar
}

ext.curseKey = System.getenv("CURSE_API_KEY")
if(invtweaks_version.endsWith("-dev")) {
    ext.curseType = 'alpha'
} else {
    ext.curseType = 'release'
}
if(curseKey) {
    curse {
        dependsOn apiJar

        displayName = 'Inventory Tweaks ' + version
        releaseType = curseType
        projectId = '223094'
        apiKey = curseKey
        changelog = getGitChangelog()
        gameVersions.add(mcversion)

        additionalArtifact apiJar
    }
}

processResources {
    inputs.property "version", internal_version
    inputs.property "mcversion", mcversion

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.png'
        exclude '**/*_at.cfg'

        expand version: internal_version, mcversion: mcversion
    }
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.png'
        include '**/*_at.cfg'
    }
}


idea {
    module {
        inheritOutputDirs = true
    }

    project {
        vcs = 'Git'
    }
}
